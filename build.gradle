import org.bxteam.runserver.RunServerTask

plugins {
    id 'java'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id 'xyz.jpenilla.run-velocity' version "2.3.1"
    id 'org.bxteam.runserver' version '1.2.2'
}

group = 'me.itstautvydas'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    implementation("org.projectlombok:lombok:1.18.38")

    implementation('net.md-5:bungeecord-api:1.21-R0.4-SNAPSHOT')
    implementation("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")

    annotationProcessor("org.projectlombok:lombok:1.18.38")
    annotationProcessor("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
}

tasks {
    runVelocity {
        // Configure the Velocity version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        velocityVersion("3.4.0-SNAPSHOT")
    }
}

def targetJavaVersion = 17
java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

tasks.register('runBungeeCord', RunServerTask) {
    serverType = "BUNGEECORD"
    serverVersion("1.21")
    noGui(true)
    acceptMojangEula()
}

tasks.register('runPaper', RunServerTask) {
    serverType = "PAPER"
    serverVersion("1.21.8")
    noGui(true)
    acceptMojangEula()
}

tasks.register('runFolia', RunServerTask) {
    serverType = "FOLIA"
    serverVersion("1.21.8")
    noGui(true)
    acceptMojangEula()
}